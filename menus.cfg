// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

teammode = 0
mode_type = 0
recording = 0
address = "server_address"
port = "28785"

togglemainmenu = [
    if (! (cleargui 1)) [
	showgui main
    ]
]

bind ESCAPE [togglemainmenu]

//MAIN MENU

newgui main [
    spec = [spectator (! (isspectator (getclientnum)))]
    guilist [
	guitext "^f4Name : " (playermodelicon)
	newname = (getname)
	guifield newname 15 [name $newname]
    ]
    guibutton (concat "^f4Character :" (playermodelname)) [guirolloveraction = (playermodelbutton $playermodel); showgui playermodel] (playermodelicon)
    guilist [
	guibutton "^f4Crosshair : " [showgui crosshair] (playermodelicon)
	guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guibar
    if (isconnected) [
	if (|| $editing (m_edit $getmode)) [ 
	    guibutton "Editing menu" "showgui editing" 
	    guibar
	]
    ]
    guibutton "Join a server" "showgui servers" "action"
    if (isconnected) [
	guibutton "Master"		"showgui master"	"action"
	guibutton "Vote mode/map"	"showgui gamemode"	"action"

	if (&& (m_teammode $getmode) (! $isspectator)) [
	    if (strcmp $getteam "good") [
		guibutton "Switch to team Evil" "team evil"	"radio_on"
	    ] [
		guibutton "Switch to team Good" "team good"	"radio_on"
	    ]
	]

	if (isspectator $getclientnum) [guibutton "^f0Join" $spec (playermodelicon)] [guibutton "^f4Spectate" $spec "spectator"]

	guibar
	guibutton "Settings"		"showgui settings"
	guibutton "^f2Record"		"showgui record_settings"	"radio_on"
	guibutton "^f3Disconnect"	"disconnect"			"exit"
    ] [
	guibutton "Singleplayer"	"showgui singleplayer"		"action"
	guibar
	guibutton "Settings"		"showgui settings"
	guibutton "^f2About"		"showgui about"			"radio_on"
	guibutton "^f3Quit"		"quit"				"exit"
    ]
] 0


//About menu
newgui about [
    guititle "Cube 2: Sauerbraten"
    guitext [by Wouter "Aardappel" van Oortmerssen, Lee "eihrul" Salzman,]
    guitext [Mike "Gilt" Dysart, Robert "baby-rabbit" Pointon,]
    guitext [John "geartrooper" Siar, Quinton "Quin" Reeves, and others]
    guitext "(for a full list of contributors see the readme)"
    guitext "http://sauerbraten.org/" "radio_on"
] 0

//This generates maps lists with a list of name
genmapitems = [
    looplist curmap $arg1 [
	guibutton $curmap (concat map $curmap)	"cube"
    ]
]

//OLD ORGANISATION
ffamaps1 = "aard3c academy akaritori alithia alloy aqueducts arbana bvdm_01 castle_trap collusion complex corruption curvedm curvy_castle darkdeath deathtek depot"
ffamaps2 = "dirtndust DM_BS1 dock douze duel7 duel8 dune elegy fanatic_quake force fragplaza frostbyte frozen fury guacamole gubo hades"
ffamaps3 = "hashi hog2 industry injustice island justice kalking1 katrez_d kffa killfactory kmap5 konkuri-to ksauer1 legazzo lostinspace masdm mbt10"
ffamaps4 = "mbt2 mbt9 memento metl2 metl3 metl4 moonlite neondevastation neonpanic nmp8 nucleus oasis oddworld ogrosupply orbe orion osiris"
ffamaps5 = "ot outpost paradigm park pgdm phosgene pitch_black powerplant refuge renegade rm5 roughinery ruby ruine sauerstruck sdm1 shadowed"
ffamaps6 = "shindou shinmei1 shiva simplicity skrdm1 stemple suburb tartech teahupoo tejen thetowers thor torment tumwalk turbine wake5 wdcd"
capturemaps1 = "abbey akroseum alithia arabic asgard asteroids c_egypt c_valley campo capture_night caribbean collusion core_refuge core_transfer corruption cwcastle damnation"
capturemaps2 = "dirtndust donya duomo dust2 eternal_valley evilness face-capture fb_capture fc3 fc4 fc5 forge frostbyte hades hallo haste hidden"
capturemaps3 = "infamy killcore3 kopenhagen lostinspace mbt12 mercury monastery nevil_c nitro nmp4 nmp8 nmp9 nucleus ogrosupply paradigm ph-capture reissen"
capturemaps4 = "relic river_c serenity snapper_rocks spcr subterra suburb tempest tortuga turbulence twinforts urban_c valhalla venice xenon"
ctfmaps1 = "abbey akroseum arbana asgard authentic autumn bad_moon berlin_wall bt_falls campo capture_night catch22 core_refuge core_transfer damnation desecration dust2"
ctfmaps2 = "eternal_valley europium evilness face-capture flagstone forge forgotten garden hallo haste hidden infamy kopenhagen l_ctf mach2 mbt1 mbt12"
ctfmaps3 = "mbt4 mercury mill nitro nucleus recovery redemption reissen sacrifice shipwreck siberia snapper_rocks spcr subterra suburb tejen tempest"
ctfmaps4 = "tortuga turbulence twinforts urban_c valhalla wdcd xenon"
conceptmaps = "ladder spiralz canyon secondevermap firstevermap door_demo box_demo platform_demo"
spmaps = "mpsp6a mpsp6b mpsp6c mpsp9a mpsp9b mpsp9c mpsp10 lost level9"
allmaps = (concat $ffamaps1 $ffamaps2 $ffamaps3 $ffamaps4 $ffamaps5 $ffamaps6 $capturemaps1 $capturemaps2 $capturemaps3 $capturemaps4 $ctfmaps1 $ctfmaps2 $ctfmaps3 $ctfmaps4 $conceptmaps $spmaps)
//OLD ORGANISATION [END]

//ALL MAPS LISTS (ffa, coop-edit, teamplay, instagib, instagib team, efficiency, efficiency team, tactics, tactics team, dmsp, sp)
alltiny1 = "aard3c academy complex corruption curvy_castle darkdeath dock douze duel7 duel8"
alltiny2 = "dune elegy force fragplaza frozen island justice kffa legazzo neondevastation"
alltiny3 = "ot orion park pgdm refuge renegade rm5 sauerstruck sdm1 shindou"
alltiny4 = "shinmei1 shiva simplicity stemple tartech teahupoo torment tumwalk turbine wake5"
allsmall1 = "alithia arabic asteroids authentic autumn bad_moon bvdm_01 c_egypt c_valley campo capture_night collusion curvedm"
allsmall2 = "fanatic_quake fc3 frostbyte fury garden guacamole hades hashi hog2 industry kalking1 kmap5 ksauer1"
allsmall3 = "lostinspace mbt1 mbt2 mbt10 memento metl2 metl3 metl4 moonlite nevil_c nmp4 nmp8 oasis"
allsmall4 = "oddworld orbe osiris outpost phosgene pitch_black powerplant relic roughinery shadowed skrdm1 twinforts wdcd"
allmedium1 = "abbey aqueducts alithia alloy arbana bt_falls castle_trap catch22 cwcastle damnation deathtek desecration dirtndust DM_BS1"
allmedium2 = "dust2 eternal_valley evilness face-capture fc4 fc5 forge forgotten frostbyte gubo hades haste infamy injustice"
allmedium3 = "katrez_d killcore3 killfactory l_ctf lostinspace masdm mbt4 mbt9 mbt12 mercury monastery nitro nucleus ogrosupply"
allmedium4 = "paradigm recovery redemption reissen ruby ruine serenity spcr subterra suburb tejen tempest thetowers turbulence"
alllarge1 = "akaritori akroseum asgard berlin_wall caribbean core_refuge core_transfer donya duomo europium fb_capture flagstone hallo hidden konkuri-to"
alllarge2 = "kopenhagen mach2 mill ph-capture river_c sacrifice shipwreck siberia snapper_rocks thor tortuga urban_c valhalla venice xenon"

//CTF MAPS LISTS (ctf, insta ctf, efficiency ctf, protect, insta protect, efficiency protect, collect, insta collect, efficiency collect)

ctfsmall1 = "abbey arbana authentic autumn bad_moon campo"
ctfsmall2 = "capture_night garden haste mbt1 twinforts wdcd"
ctfmedium1 = "bt_falls catch22 core_transfer damnation desecration dust2 eternal_valley evilness forge forgotten infamy l_ctf mbt4"
ctfmedium2 = "mbt12 mercury nitro nucleus recovery redemption reissen spcr subterra suburb tejen tempest turbulence"
ctflarge1 = "akroseum asgard berlin_wall core_refuge europium face-capture flagstone hallo hidden kopenhagen"
ctflarge2 = "mach2 mill sacrifice shipwreck siberia snapper_rocks tortuga urban_c valhalla xenon"

//CAPTURE MAPS LISTS (capture, regen capture, hold, insta hold, efficiency hold)

capturesmall1 = "abbey alithia arabic asteroids campo c_egypt c_valley capture_night corruption fc3"
capturesmall2 = "frostbyte hades lostinspace nevil_c nmp4 nmp8 nmp9 reissen relic twinforts"
capturemedium1 = "collusion cwcastle damnation dirtndust dust2 evilness face-capture fc4 fc5 forge haste killcore3 mbt12"
capturemedium2 = "mercury monastery nevil_c nitro nucleus ogrosupply paradigm serenity spcr subterra suburb tempest turbulence"
capturelarge1 = "akroseum asgard core_refuge core_transfer caribbean donya duomo eternal_valley fb_capture hallo hidden"
capturelarge2 = "infamy kopenhagen ph-capture river_c snapper_rocks tortuga urban_c valhalla venice xenon"


//This generates the map details while pointing to a map. Includes : a guibar, the map picture and the name of the current map (plus a "Select a map" sentence)
showmapdetails = [
    guibar
    guistrut 1 0
    guilist [
	guititle $guirollovername
	guistrut 1 0
	guiimage (concatword "packages/base/" (if (> $numargs 0) [result $arg1] [at $guirollovername 0])".jpg") $guirolloveraction 4 1 "data/cube.png"
    ]
]


//All maps menu (ffa, coop-edit, teamplay, instagib, instagib team, efficiency, efficiency team, tactics, tactics team, dmsp, sp)
newgui maps [
    guistrut 14 1
    guititle "^f0All maps | Tiny"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $alltiny1 ]
	guistrut 1 0
	guilist [ genmapitems $alltiny2 ]
	showmapdetails
    ]
    guitab "2"
    guititle "^f0All maps | Tiny"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $alltiny3 ]
	guistrut 1 0
	guilist [ genmapitems $alltiny4 ]
	showmapdetails
    ]
    guitab "Small"
    guititle "^f1All maps | Small"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $allsmall1 ]
	guistrut 1 0
	guilist [ genmapitems $allsmall2 ]
	showmapdetails
    ]
    guitab "2"
    guititle "^f1All maps | Small"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $allsmall3 ]
	guistrut 1 0
	guilist [ genmapitems $allsmall4 ]
	showmapdetails
    ]
    guitab "Medium"
    guititle "^f6All maps | Medium"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $allmedium1 ]
	guistrut 1 0
	guilist [ genmapitems $allmedium2 ]
	showmapdetails
    ]
    guitab "2"
    guititle "^f6All maps | Medium"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $allmedium3 ]
	guistrut 1 0
	guilist [ genmapitems $allmedium4 ]
	showmapdetails
    ]
    guitab "Large"
    guititle "^f3All maps | Large"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $alllarge1 ]
	guistrut 1 0
	guilist [ genmapitems $alllarge2 ]
	showmapdetails
    ]
    guitab "Conceptual"
    guititle "^f5Conceptual maps"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $conceptmaps ]
	showmapdetails
    ]
] "Tiny" //HACK : this makes the first tab be named "Tiny"


//Capture The Flag maps menu (ctf, insta ctf, efficiency ctf, protect, insta protect, efficiency protect, collect, insta collect, efficiency collect)
newgui ctfmaps [
    guistrut 14 1
    guititle "^f1CTF maps | Small"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $ctfsmall1 ]
	guistrut 1 0
	guilist [ genmapitems $ctfsmall2 ]
	showmapdetails
    ]
    guitab "Medium"
    guititle "^f6CTF maps | Medium"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $ctfmedium1 ]
	guistrut 1 0
	guilist [ genmapitems $ctfmedium2 ]
	showmapdetails
    ]
    guitab "Large"
    guititle "^f3CTF maps | Large"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $ctflarge1 ]
	guistrut 1 0
	guilist [ genmapitems $ctflarge2 ]
	showmapdetails
    ]
] "Small"


//Capture maps menu (capture, regen capture, hold, insta hold, efficiency hold)
newgui capturemaps [
    guistrut 14 1
    guititle "^f1Capture maps | Small"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $capturesmall1 ]
	guistrut 1 0
	guilist [ genmapitems $capturesmall2 ]
	showmapdetails
    ]
    guitab "Medium"
    guititle "^f6Capture maps | Medium"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $capturemedium1 ]
	guistrut 1 0
	guilist [ genmapitems $capturemedium2 ]
	showmapdetails
    ]
    guitab "Large"
    guititle "^f3Capture maps | Large"
    guistrut 1 0
    guilist [
	guilist [ genmapitems $capturelarge1 ]
	guistrut 1 0
	guilist [ genmapitems $capturelarge2 ]
	showmapdetails
    ]
] "Small"

custommaps = ""
newgui custommaps [
    guititle (concat "^f3" $guirollovername)
    guibar
    looplist curmap $custommaps [
	guibutton $curmap (concat map $curmap) "cube"
    ]
] "custom maps"

showcustommaps = [
    custommaps = ""
    loopfiles curmap "packages/base" "ogz" [
	if (< (indexof $allmaps $curmap) 0) [
	    custommaps = (concat $custommaps $curmap)
	]
    ]
    custommaps = (sortlist $custommaps x y [<=s $x $y])
    showgui custommaps
]
macro playermodelbutton [cleargui 1; playermodel %1]


//Playermodel choice menu
newgui playermodel [
    color = 0
    guilist [
	guilist [
	    guistrut 10 1
	    guititle "^f4    Characters    "
	    guistrut 1 0
	    loop i $playermodelnum [
		guibutton (playermodelname $i) (playermodelbutton $i) (playermodelicon $i)
	    ]
	]
	guibar
	pmidx = (substr $guirolloveraction (strlen (playermodelbutton "")))
	pmidx = (max 0 (min (- $playermodelnum 1) $pmidx))
	guilist [
	    guiplayerpreview $pmidx 0 6 $guirolloveraction 4 2
	    guitextbox (playermodelstory $pmidx) 40 10
	]
    ]
] 0

crosshairs = ["data/crosshair.png data/hit.png" 
"packages/crosshairs/x.png packages/crosshairs/x-hit.png"
"packages/crosshairs/x_dot.png packages/crosshairs/x_dot-hit.png"
"packages/crosshairs/o.png packages/crosshairs/o-hit.png"
"packages/crosshairs/o_x.png packages/crosshairs/o_x-hit.png"
"packages/crosshairs/o_dot.png packages/crosshairs/o_dot-hit.png"
"packages/crosshairs/dot.png packages/crosshairs/dot-hit.png"
"packages/crosshairs/dot_wide.png packages/crosshairs/dot_wide-hit.png"
"packages/crosshairs/star.png packages/crosshairs/star-hit.png"
"packages/crosshairs/wide.png packages/crosshairs/wide-hit.png"
"packages/crosshairs/circle_dot.png packages/crosshairs/circle_dot_hit.png"
"packages/crosshairs/cross_normal.png packages/crosshairs/cross_normal_hit.png"
"packages/crosshairs/cross_whole.png packages/crosshairs/cross_whole_hit.png"
"packages/crosshairs/dot_normal.png packages/crosshairs/dot_normal_hit.png"
"packages/crosshairs/dot_whole.png packages/crosshairs/dot_whole_hit.png"
]


//Crosshairs choice menu
newgui crosshair [
    guitext "^f7Size"
    guislider crosshairsize
    loop row (div (+ (listlen $crosshairs) 6) 7) [
	guilist [
	    loop col 7 [
		ch = (at $crosshairs (+ (* $row 7) $col))
		if (!=s $ch "") [
		    guiimage (at $ch 0) [ 
			cleargui 1
			loadcrosshair @(at $ch 0) 0
			loadcrosshair @(at $ch (if (> (listlen $ch) 1) 1 0)) 2
		    ] 0.5
		]
	    ]
	]
    ]
] 0


//Master mode menu (while in-game)
newgui master [
    guistayopen [
	if (ismaster $getclientnum) [
	    guibutton "Relinquish master"	"setmaster 0"	"radio_on"
	] [
	    guibutton "Claim master"		"setmaster 1"	"radio_off"
	]
	if (ismaster $getclientnum) [
	    guilist [
		guitext "Mastermode : "
		mm = (getmastermode)
		guiradio "^f0open (0) "		mm 0		"mastermode 0"
		guiradio "^f2veto (1) "		mm 1		"mastermode 1"
		guiradio "^f6locked (2) "	mm 2		"mastermode 2"
		guiradio "^f3private (3) "	mm 3		"mastermode 3"
	    ]
	]
	guibar
	if (ismaster $getclientnum) [
	    looplist cn (listclients 0 0) [
		guilist [
		    guitext (concatword (getclientname $cn) ": ") (getclienticon $cn)
		    guibutton "Kick " [kick @cn] "exit"
		    if (isspectator (@cn)) [
			guibutton "Join " [spectator @(= (isspectator $cn) 0) @cn] (playermodelicon)] [
			guibutton "Spectate " [spectator @(= (isspectator $cn) 0) @cn] "spectator"
		    ]
		    if (m_teammode $getmode) [
			if (=s (getclientteam $cn) "good") [
			    guibutton "Switch to Evil " [setteam @cn "evil"] "radio_on"
			] [
			    guibutton "Switch to Good " [setteam @cn "good"] "radio_on"
			]
		    ]
		    if (ismaster $cn) [
			guibutton "Take master" [setmaster 0 @cn] "radio_on"
		    ] [
			guibutton "Give master" [setmaster 1 @cn] "radio_on"
		    ] 
		]
	    ]
	]
    ]
] 0

botmatchcount = 5
botmatchminskill = 25
botmatchmaxskill = 75

botmatch = [
    if (isconnected 0 0) [
	echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
	loop i $botmatchcount [addbot (rnd (+ $botmatchmaxskill 1) $botmatchminskill)]
    ]
]

startbotmatch = [
    if (isconnected 0 0) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $botmatchcount [addbot (rnd (+ $botmatchmaxskill 1) $botmatchminskill)]
    ]
]


//Singleplayer menu (accessible via main-menu while not in-game)
newgui singleplayer [
    guititle "Singleplayer game modes"
    guibar
    guilist [
	guilist [
	    guititle "^f2   Bot match :   "
	    guistrut 1 0
	    guitext "Bots amount :"
	    guislider botmatchcount 0 32
	    guitext "Minimum skill :"
	    guislider botmatchminskill 25 101 [if (< $botmatchmaxskill $botmatchminskill) [botmatchmaxskill = $botmatchminskill]]
	    guitext "Maximum skill :"
	    guislider botmatchmaxskill 25 101 [if (> $botmatchminskill $botmatchmaxskill) [botmatchminskill = $botmatchmaxskill]]
	    guilist [ guibutton "^f6Start" [guionclear [startbotmatch]; showgui gamemode]	"sauer" ]
	]
	guistrut 2 0
	guilist [
	    guilist [ guistrut 1 0; guititle "^f2Campaign mode :" ]
	    guistrut 1 0
	    guilist [ guistrut 1 0; guibutton "^f6Campaigns"	"showgui campaigns"	"sauer" ]
	    guistrut 1 0
	    guilist [ guistrut 1 0; guibutton "^f6DMSP match"	"mode -2; showgui maps"	"sauer" ]
	    guilist [ guistrut 1 0; guicheckbox "^f7Slow motion"		"slowmosp" ]
	    guilist [ guistrut 1 0; guitext   "Skill (default: 3)" ]
	    guislider skill
	]
    ]
] 0


//Campaigns menu (accessible via singleplayer menu)
newgui campaigns [
    guititle "Campaign : ^f3Private Stan Sauer"
    guistrut 1 0
    guilist [
	guilist [
	    guibutton "Run N' Gun Part I" "sp mpsp9a" "cube"
	    guibutton "^f2Run N' Gun Part II" "sp mpsp9b" "cube"
	    guibutton "^f6Run N' Gun Part III" "sp mpsp9c" "cube"
	    guibutton "^f3THE SERIOUSLY BIG VALLEY" "sp mpsp10" "cube"
	]
	showmapdetails (substr $guirolloveraction 3)
    ]
    guitab "An Army Of One"
    guititle "Campaign : ^f3An Army Of One"
    guistrut 1 0
    guilist [
	guilist [
	    guibutton "Part I           " "sp mpsp6a" "cube"
	    guibutton "^f2Part II          " "sp mpsp6b" "cube"
	    guibutton "^f6Part III         " "sp mpsp6c" "cube"
	]
	showmapdetails (substr $guirolloveraction 3)
    ]
    guitab "^f2Lost & Level 9"
    guititle "Campaigns : ^f5Lost^f~ & ^f3Level 9"
    guistrut 1 0
    guilist [
	guilist [
	    guibutton "^f5Lost"	"sp lost"	"cube"
	    guibutton "^f3Level 9"	"sp level9"	"cube"
	]
	showmapdetails (substr $guirolloveraction 3)
	guilist [
	    guistrut 1 0
	    guistrut 1 0
	    guitext	"^f7Go threw the secrets of" "blank.png"
	    guitext	"^f7those space stations" "blank.png"
	]
    ]
] "Private Stan Sauer"


//Servers list menu (accessible via main-menu)
newgui servers [
    guistayopen [
	guiservers [
	    guistrut
	    guilist [
		guitext "^f4Address : "
		guifield address 16 [address = $address]
		guistrut 1 0
		guitext "^f4Port : "
		guifield port 5 [port = $port]
		guistrut 1 0
		guistrut 1 0
		guibutton "^f2Connect" (concat "connect" $address $port)
	    ]
	    guibar
	    guilist [
		guibutton "Refresh list" "updatefrommaster"
		guistrut 1 0
		guicheckbox "Show local servers" searchlan
		guistrut 1 0
		guicheckbox "Auto-refresh" autoupdateservers
		guistrut 1 0
		guicheckbox "Auto-sort" autosortservers
		if (= $autosortservers 0) [
		    guibar
		    guibutton "Sort" "sortservers"
		]
	    ]
	    guibar
	] 10
    ]
] "Servers" [initservers]


//Game modes menu (accessible via singleplayer menu when launchin a bot match)
newgui gamemode [
    if (= $teammode 0) [
	guititle "Game modes"
    ] [
	guititle "^f6Game modes : Teamplay"
    ]
    guistrut 1 0
    guibutton "^f6Coop-Edit"		"mode 1; showgui maps"
    guistrut 0.5 0
    guilist [
	if (= $mode_type 0) [
	    if (= $teammode 0) [
		guilist [
		    guibutton	"^f2DeathMatch"		"mode 0; showgui maps"
		    guibutton	"^f2Tactics"		"mode 7; showgui maps"
		    guitext	"^f4CTF"		"blank.png"
		    guitext	"^f4Protect"		"blank.png"
		]
		guistrut 1 0
		guilist [
		    guitext	"^f4Collect"		"blank.png"
		    guitext	"^f4Hold"		"blank.png"
		    guitext	"^f4Capture"		"blank.png"
		    guitext	"^f4Regen Capture"	"blank.png"
		]
	    ] [
		guilist [
		    guibutton	"^f2DeathMatch"		"mode 2; showgui maps"
		    guibutton	"^f2Tactics"		"mode 8; showgui maps"
		    guibutton	"^f2CTF"		"mode 11; showgui ctfmaps"
		    guibutton	"^f2Protect"		"mode 13; showgui ctfmaps"
		]
		guistrut 1 0
		guilist [
		    guibutton	"^f2Collect"		"mode 20; showgui ctfmaps"
		    guibutton	"^f2Hold"		"mode 15; showgui capturemaps"
		    guibutton	"^f2Capture"		"mode 9; showgui capturemaps"
		    guibutton	"^f2Regen capture"	"mode 10; showgui capturemaps"
		]
		guistrut 0.2 0 //Keeps the gui to the same size on this part
	    ]
	]

	if (= $mode_type 1) [
	    if (= $teammode 0) [
		guilist [
		    guibutton	"^f1DeathMatch"		"mode 3; showgui maps"
		    guitext	"^f4Tactics"		"blank.png"
		    guitext	"^f4CTF"		"blank.png"
		    guitext	"^f4Protect"		"blank.png"
		]
		guistrut 1 0
		guilist [
		    guitext	"^f4Collect"		"blank.png"
		    guitext	"^f4Hold"		"blank.png"
		    guitext	"^f4Capture"		"blank.png"
		    guitext	"^f4Regen Capture"	"blank.png"
		]
	    ] [
		guilist [
		    guibutton	"^f1DeathMatch"		"mode 4; showgui maps"
		    guitext	"^f4Tactics"		"blank.png"
		    guibutton	"^f1CTF"		"mode 12; showgui ctfmaps"
		    guibutton	"^f1Protect"		"mode 14; showgui ctfmaps"
		]
		guistrut 1 0
		guilist [
		    guibutton	"^f1Collect"		"mode 21; showgui ctfmaps"
		    guibutton	"^f1Hold"		"mode 16; showgui capturemaps"
		    guitext	"^f4Capture"		"blank.png"
		    guitext	"^f4Regen Capture"	"blank.png"
		]
	    ]
	]

	if (= $mode_type 2) [
	    if (= $teammode 0) [
		guilist [
		    guibutton	"^f0DeathMatch"		"mode 5; showgui maps"
		    guitext	"^f4Tactics"		"blank.png"
		    guitext	"^f4CTF"		"blank.png"
		    guitext	"^f4Protect"		"blank.png"
		]
		guistrut 1 0
		guilist [
		    guitext	"^f4Collect"		"blank.png"
		    guitext	"^f4Hold"		"blank.png"
		    guitext	"^f4Capture"		"blank.png"
		    guitext	"^f4Regen Capture"	"blank.png"
		]
	    ] [
		guilist [
		    guibutton	"^f0DeathMatch"		"mode 6; showgui maps"
		    guitext	"^f4Tactics"		"blank.png"
		    guibutton	"^f0CTF"		"mode 17; showgui ctfmaps"
		    guibutton	"^f0Protect"		"mode 18; showgui ctfmaps"
		]
		guistrut 1 0
		guilist [
		    guibutton	"^f0Collect"		"mode 22; showgui ctfmaps"
		    guibutton	"^f0Hold"		"mode 19; showgui capturemaps"
		    guitext	"^f4Capture"		"blank.png"
		    guitext	"^f4Regen Capture"	"blank.png"
		]
	    ]
	]
    ]
    guibar
    guilist [
	guicheckbox "Teamplay"	teammode
	guistrut 1 0
	guilist [
	    guiradio	"^f2Classic"	mode_type 0
	    guiradio	"^f1Instagib"	mode_type 1
	    guiradio	"^f0Efficiency"	mode_type 2
	]
    ]
] 0


//This creates a menu text editor for a fixed text file ($arg1) and with a fixed size ($arg $arg3)
showfileeditor = [
    guinoautotab [
	guieditor $arg1 $arg2 $arg3
	textinit $arg1 $arg1
	guistayopen [
	    guibar
	    guilist [
		guibutton "^f1Load" [textfocus @arg1; textload @arg1]
		guistrut 2 0
		guibutton "^f0Save" [textfocus @arg1; textsave @arg1]
		guistrut 2 0
		guibutton "Exec" [textfocus @arg1; textexec]
		guistrut 2 0
		guibutton "Copy" [textfocus @arg1; textcopy]
		guistrut 2 0
		guibutton "Paste" [textfocus @arg1; textpaste]
		guistrut 2 0
		guibutton "Select" [textfocus @arg1; textselectall]
		guistrut 2 0
		guibutton "^f3Clear" [textfocus @arg1; textclear]	"exit"
	    ]
	]
    ]
]

notepadfile = "untitled.txt"

newgui notepad [
    guifield notepadfile -30
    showfileeditor $notepadfile -80 20
] 0

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showgui notepad
]

newgui pastebuffer [
    guinoautotab [
	guieditor "#pastebuffer" -80 20
	guistayopen [
	    guilist [
		guibutton "Exec" [textfocus "#pastebuffer"; textexec]
		guistrut 1 0
		guibutton "^f3Clear" [textfocus "#pastebuffer"; textclear]
	    ]
	]
    ]
] 0

pastebuffer = [showgui pastebuffer]

newgui scratchpad [
    guinoautotab [
	guieditor "#scratchpad" -80 20
	guistayopen [
	    guilist [
		guibutton "Exec" [textfocus "#scratchpad"; textexec]
		guistrut 1 0
		guibutton "Copy" [textfocus "#scratchpad"; textcopy]
		guistrut 1 0
		guibutton "Paste" [textfocus "#scratchpad"; textpaste]
		guistrut 1 0
		guibutton "Select" [textfocus "#scratchpad"; textselectall]
		guistrut 1 0
		guibutton "^f3Clear" [textfocus "#scratchpad"; textclear]
	    ]
	]
    ]
] 0

scratchpad = [showgui scratchpad]

newmapsize = 12
savemap_name = temp



skies1 = [
aftas/sky/orbe
blindabuser/blindasky
blindabuser/blindasky2
darc/forge
dash/moon
hazel/green
humus/meadow
ik2k/env/iklake
jon/prarie
mayhem/duomo
mayhem/eclipse
meister/uzze
penguins/arid
penguins/harmony
penguins/wrath
penguins/yonder
ratboy/skyboxes/coward
]
skies2 = [
shmutzwurst/london
skyboxes/clearsky052
skyboxes/darkness
skyboxes/evilsky
skyboxes/morning
skyboxes/remus/sky01
skyboxes/turbulent
socksky/barren
socksky/desert
socksky/emerald
socksky/frozen
socksky/frozendusk
socksky/grave
socksky/mars
socksky/nightball
socksky/valley
staffy/staffy
]


//Generates the list of skyboxes with lists
genskyitems = [
    looplist cursky $arg1 [
	guibutton $cursky (concat skybox $cursky) "cube"
    ]
]



showskydetails = [ 
    guistrut 1 0
    guibar
    guilist [
	guistrut 1 0
	guititle $guirollovername
	guistrut 1 0
	guiimage (concatword "<thumbnail:128>packages/" (if (> $numargs 0) [result $arg1] [result (at $guirollovername 0)]) "_ft.jpg") $guirolloveraction 4 1 "data/cube.png"
    ]
]

enableediting = [ guibutton "^f2Toggle Edit mode (E)"	"edittoggle"	(playermodelicon) ]

//EDITING MENU
editingcheck = [
    if (! $editing) [
	guistayopen [
	    enableediting
	    guistrut 0.5 0
	]
    ]
]

newgui editing [
    guistayopen [
	enableediting
    ]
    guistrut 0.5 0
    guibutton "Keys"		"showgui editing_keys"		"radio_on"
    guibutton "Light"		"showgui editing_lights"	"radio_on"
    guibutton "Entities"	"showgui editing_entities"	"radio_on"
    guibutton "Blend"		"showgui editing_blend"		"radio_on"
    guibutton "Skyboxes"	"showgui editing_skyboxes"	"radio_on"
    guibutton "Map"		"showgui editing_map"		"radio_on"
    guibutton "Console"		"showgui editing_console"	"radio_on"
] 0

newgui editing_entities [
    editingcheck
    guibutton	"Entities list"		"showgui entlist"	"sauer"
    guistrut 1 0
    guibutton	"Drop entities"		"showgui dropent"	"action"
    guibutton	"Mapmodels"		"showgui mapmodels"	"action"
    guibutton	"Teleports"		"showgui newteleport"	"action"
    guibutton	"Monsters"		"showgui monsters"	"action"
    guibutton	"Triggers"		"showgui triggers"	"action"
    guibutton	"Materials"		"showgui materials"	"action"
] 0

newgui editing_keys [
    editingcheck
    guilist [
	guilist [
	    guibutton "Copy				(C)"	"copy"
	    guibutton "Delete			(BACKSPACE)"		"editdel"
	    guibutton "Deselect			(SPACE)"	"cancelsel"
	    guibutton "Edit entity console	(PERIOD)"		"selentedit"
	    guibutton "Edit entity gui		(F3)"			"showentgui"
	    guitext "Extend selection	Right mouse"
	    guibutton "Flip / mirror cube	(X)"			"flip"
	    guibutton "Mapmodels list		(F4)"			"showgui mapmodels"
	    guitext "Move selection		Drag right click"
	    guibutton "Passthrough		(hold LCTRL)"	"passthrough"
	    guibutton "Paste			(V)"		"paste"
	    guibutton "Redo				(I)"		"redo"
	]
	guistrut 1 0
	guilist [
	    guitext "Reorient				Right click"
	    guitext "Select				Drag left click"
	    guibutton "Select all in box		(keypad ENTER)"		"entselect insel"
	    guibutton "Select all matching		(N)"			"selentfindall"
	    guitext "Select corners			Drag middle click"
	    guibutton "Texture palette		(F2)"				"showtexgui"
	    guitext "Toggle heightmap		(H)"
	    guitext "Toggle heightmap tex	(MMB in heightmapmode)"
	    guicheckbox "Toggle outline			(7)"	outline
	    guicheckbox "Toggle wireframe		(8)"    wireframe
	    guibutton "Undo					(Z, U)"		"undo"
	]
    ]

    guitab "Scroll"
    editingcheck
    guibutton "Center view on ent			Hold COMMA + scroll wheel"	"entautoview"
    guitext "Change heightmap brush		Hold B + scroll wheel"
    guitext "Change gridsize			Hold G + scroll wheel"
    guitext "Change textures			Hold Y + scroll wheel"
    guitext "Push/pull heightmap			Hold LALT + scroll wheel"
    guitext "Push/pull selection			Scroll wheel only"
    guitext "Push corner with cursor		Hold Q + scroll wheel"
    guitext "Push the face of a cube	Hold F + scroll wheel"
    guitext "Rotate					Hold R + scroll wheel"
] "Keys"

newgui editing_lights [
    editingcheck
    guibutton "^f2New Light"		"resetlight;showgui newlight"
    guibar
    guibutton "Calclight 1 (slow: 8xAA)"			"calclight 1"
    guibutton "Calclight -1 (quick: no AA, no model shadows)"	"calclight -1"
    guibutton "Patchlight"					"patchlight"

    guicheckbox "Fullbright" fullbright

    guitext "Lightprecision (default: 32)"
    guilistslider lightprecision "8 16 32 48 64 128 256"

    guitext "Lighterror (default: 8)"
    guislider lighterror

    guitext "Lightthreads (CPU threads/cores) (default: 0)"
    guislider lightthreads
] 0

newgui editing_blend [
    editingcheck
    guitext "Blend paint mode :"
    guiradio "Off						(keypad 0)"	blendpaintmode 0
    guiradio "Replace					(keypad 1)"	blendpaintmode 1
    guiradio "Dig						(keypad 2)"	blendpaintmode 2
    guiradio "Fill						(keypad 3)"	blendpaintmode 3
    guiradio "Inverted dig				(keypad 4)"		blendpaintmode 4
    guiradio "Inverted fill				(keypad 5)"		blendpaintmode 5
    guibar
    guitext "Paint blendmap			(MOUSE1)"
    guibutton "Change blend brush			scroll wheel" "nextblendbrush"
    guibutton "Rotate blend brush			(MOUSE2)" "rotateblendbrush"
    guibutton "Clear entire blendmap 		(^"clearblendmap^" command)" "clearblendmap"
    guibutton "Clear blendmap in selection	(^"clearblendmapsel^" command)" "clearblendmapsel"
] 0

newgui editing_map [
    guitext "Save map as :"
    guifield  savemap_name 20 [ savemap $savemap_name ]
    guibutton [Load @savemap_name map]			"map $savemap_name"
    guibutton "Create a new map (default size: 12)"		"newmap $newmapsize"
    guislider "newmapsize" 10 16
    guibutton "Increase map size (2x)"			"mapenlarge"
    guitext "Max undo size (default: 5 MB)"
    guislider "undomegs" 1 10
    guibutton "Optimize geometry"			"remip"
    guibutton "Set map title/author"			"saycommand [/maptitle ]"
    guistrut 1 0
    guibutton "^f2Show custom maps"			"showcustommaps"	"cube"
] 0

newgui editing_skyboxes [
    guilist [
	guistayopen [
	    guilist [ genskyitems $skies1 ]
	    guistrut 1 0
	    guilist [ genskyitems $skies2 ]
	    showskydetails
	]
    ]
] 0

newgui editing_console [
    guititle (mapcfgname)
    guistrut 1 0
    showfileeditor (mapcfgname) -76 12
] 0



//newgui materials [
//    editingcheck
//    guibutton "air"			"editmat air"
//    guibutton "water"			"editmat water"
//    guibutton "lava"			"editmat lava"
//    guibutton "clip"			"editmat clip"
//    guibutton "glass"			"editmat glass"
//    guibutton "noclip"			"editmat noclip"
//    guibutton "gameclip"		"editmat gameclip"
//    guibutton "death"			"editmat death"
//    guibutton "alpha"			"editmat alpha"
//    guistrut 1 0
//    guicheckbox "show material volumes"	showmat 

    //guibutton "waterlod very high		(3)"	"waterlod 3"
    //guibutton "waterlod high		(2)"		"waterlod 2"
    //guibutton "waterlod default		(1)"	"waterlod 1"
    //guibutton "waterlod low		(0)"		"waterlod 0"

    //guibutton "watersubdiv very fine	(0)"		"watersubdiv 0"
    //guibutton "watersubdiv fine		(1)"	"watersubdiv 1"
    //guibutton "watersubdiv default		(2)"	"watersubdiv 2"
    //guibutton "watersubdiv coarse		(3)"	"watersubdiv 3"

    //guibutton "water waves on"			"waterwaves 1"
    //guibutton "water waves off"			"waterwaves 0"
//] 0

setting_entediting = 1

newgui dropent [
    editingcheck
    guitext "drop entities:"
    guiradio "to the floor"			entdrop 1
    guiradio "on selection"			entdrop 2
    guiradio "to the floor at selection"	entdrop 3
    guiradio "at current position"		entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"		entselsnap
    guicheckbox "entity selection"		setting_entediting 1 0 [ entediting $setting_entediting ]
] 0

newgui triggers [
    editingcheck
    guibutton [invisible]		[BTmm = -1; showgui trigger2]
    guibutton [a carrot]		[BTmm = 23; showgui trigger2]
    guibutton [switch #1]		[BTmm = 24; showgui trigger2]
    guibutton [switch #2]		[BTmm = 25; showgui trigger2]
    guibutton [door #1]			[BTmm = 26; showgui trigger2]
    guibutton [door #2]			[BTmm = 27; showgui trigger2]
    guibutton [door #3]			[BTmm = 28; showgui trigger2]
] 0

newgui trigger2 [
    editingcheck
    guibutton [animation loop]				[BTtt = 0; showgui trigger3]
    guibutton [trigger once (popback)]			[BTtt = 1; showgui trigger3]
    guibutton [trigger once (popback & rumble)]		[BTtt = 2; showgui trigger3]
    guibutton [trigger once]				[BTtt = 3; showgui trigger3]
    guibutton [trigger once (rumble)]			[BTtt = 4; showgui trigger3]
    guibutton [trigger multiple (popback)]		[BTtt = 5; showgui trigger3]
    guibutton [trigger multiple (popback & rumble)]	[BTtt = 6; showgui trigger3]
    guibutton [trigger multiple]			[BTtt = 7; showgui trigger3]
    guibutton [trigger multiple (rumble)]		[BTtt = 8; showgui trigger3]
    guibutton [door (open once)]			[BTtt = 9; showgui trigger3]
    guibutton [door (auto close)]			[BTtt = 10; showgui trigger3]
    guibutton [door (locked)]				[BTtt = 11; showgui trigger3]
    guibutton [vanishing trigger]			[BTtt = 12; showgui trigger3]
    guibutton [vanishing trigger (rumble)]		[BTtt = 13; showgui trigger3]
    guibutton [End Of Level Trigger]			[BTtt = 29; showgui trigger3]
] 0

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui trigger3 [
    editingcheck
    loop i 9 [
	tjt = (concat tag (concatword # $i))
	tjc = (concat BTti "=" $i ";" "build_trigger")
	guibutton $tjt $tjc
    ]
] 0


resetlight = [
    lightcolour = 0
    lightbright = 1
    lightset 255 255 255
    lightradius = 128
]

lightset = [
    lightr = $arg1
    lightg = $arg2
    lightb = $arg3
    if (= $lightbright 0) lightscale
]

lightscale = [
    if (= $lightbright 0) [
	lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
    ] [
	lightr = (min 255 (* $lightr 2)); lightg = (min 255 (* $lightg 2)); lightb = (min 255 (* $lightb 2))
    ]
]

lightcmd = [
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    editingcheck
    guibutton "sunlight"    "newent light 0 255 255 255"
    guibutton (lightcmd)
    guibar
    guitext "color:"
    guicolor (+ (* (+ (* $lightr 256) $lightg) 256) $lightb)
    guislider lightr 0 255
    guislider lightg 0 255
    guislider lightb 0 255
    guilist [
	guicheckbox "bright"    lightbright 1 0 [lightscale]
	guibar
	guiradio "white"	lightcolour 0 [lightset 255 255 255]
	guiradio "blue"		lightcolour 1 [lightset 192 192 255]
	guiradio "red"		lightcolour 2 [lightset 255 192 192]
	guiradio "green"	lightcolour 3 [lightset 192 255 192]
	guiradio "yellow"	lightcolour 4 [lightset 255 255 192]
	guiradio "purple"	lightcolour 5 [lightset 255 192 255]
	guiradio "turquoise"	lightcolour 6 [lightset 192 255 255]
    ]
    guitext "radius:"
    guislider lightradius 0 512
] 0

newgui newteleport [
    editingcheck
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
] 0

newgui mapmodels [
    editingcheck
    loop i (div (+ (nummapmodels) (- 17 1)) 17) [
	if (> $i 0) [
	    guitab (+ $i 1)
	]
	guilist [
	    guilist [
		loop j (min (- (nummapmodels) (* $i 17)) 17) [
		    mmidx = (+ (* $i 17) $j)
		    mmname = (mapmodelname $mmidx) 
		    if (= (strstr $mmname "mapmodels") 0) [
			mmname = (substr $mmname 10)
		    ]
		    guibutton $mmname [newent mapmodel @mmidx]
		]
	    ]
	    guispring
	    guibar
	    guimodelpreview (mapmodelname (at $guirolloveraction 2)) "mapmodel" $guirolloveraction 4 1
	]
    ]
] "Map Models"

newgui monsters [
    editingcheck
    guibutton "ogro / fireball"		"newent monster 0"
    guibutton "rhino / chaingun"	"newent monster 1"
    guibutton "ratamahatta / shotgun"	"newent monster 2"
    guibutton "slith / rifle"		"newent monster 3"
    guibutton "bauul / RL"		"newent monster 4"
    guibutton "hellpig / bite"		"newent monster 5"
    guibutton "knight / iceball"	"newent monster 6"
    guibutton "goblin / slimeball"	"newent monster 7"
    guibutton "spider / grenade"	"newent monster 8"
] 0

newgui postfx [
    editingcheck
    guibutton "(effect OFF)"		"clearpostfx"
    guibutton "bloom (subtle: 30%)"	"bloom 0.3"
    guibutton "bloom (bright: 55%)"	"bloom 0.55"
    guibutton "bloom (intense: 80%)"	"bloom 0.8"
    guibutton "rotoscope"		"rotoscope 1"
    guibutton "rotoscope + blur3"	"rotoscope 1 1"
    guibutton "rotoscope + blur5"	"rotoscope 1 2"
    guibutton "sobel"			"setpostfx sobel"
    guibutton "invert"			"setpostfx invert"
    guibutton "gbr"			"setpostfx gbr"
    guibutton "bw"			"setpostfx bw"
    guibutton "blur3"			"setpostfx hblur3; addpostfx vblur3"
    guibutton "blur5"			"setpostfx hblur5; addpostfx vblur5"
] 0

macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radio_on"] [result "radio_off"])
]


//SETTINGS MENU
newgui settings [
    guibutton "Game" "showgui game_settings" "radio_on"
    guibutton "Graphics" "showgui graphics_settings" "radio_on"
    guibutton "Display" "showgui display_settings" "radio_on"
    guibutton "Sound" "showgui sound_settings" "radio_on"
    guibutton "Controls" "showgui controls_settings" "radio_on"
    guibutton "Console" "showgui console_settings" "radio_on"
] 0


newgui game_settings [
    guicheckbox "Show scoreboard at death"	deathscore
    guicheckbox "Blood"			blood
    guilist [
	guicheckbox "Damage screen	"	damagescreen
	guistrut 1 0
	guicheckbox "Damage compass"	damagecompass
    ]
    guilist [
    guicheckbox "Hudguns			"		hudgun
	if $hudgun [
	    guistrut 1 0
	    guicheckbox "Sway				"	hudgunsway
	    guistrut 1 0
	    guicheckbox "Muzzle flash	"	muzzleflash
	    guistrut 1 0
	    guicheckbox "Muzzle light"	muzzlelight
	]
    ]
    guilist [
	guicheckbox "Ragdoll deaths		"	ragdoll
	if $ragdoll [
	    guistrut 1 0
	    guicheckbox "Keep after respawn	"	ragdollmillis 10000
	]
	guistrut 1 0
	guicheckbox "Hide dead players"	hidedead
    ]
    guitext "Ragdoll velocity multiplier"	(playermodelicon)
    guislider deadpush
    guilist [
	guicheckbox "^f2Wall clock"	wallclock
	if $wallclock [
	    guistrut 1 0
	    guicheckbox "^f224 hour"	wallclock24
	    guistrut 1 0
	    guicheckbox "^f2Seconds"	wallclocksecs
	]
    ]
    guicheckbox "^f2Show FPS"		showfps
    guicheckbox "^f22D menus" gui2d
    guilist [
	guicheckbox "Fullbright player models		" fullbrightmodels 60 0
	if $fullbrightmodels [
	    guistrut 1 0
	    guiradio "Subtle" fullbrightmodels 60
	    guistrut 1 0
	    guiradio "Bright" fullbrightmodels 100
	    guistrut 1 0
	    guiradio "Overbright" fullbrightmodels 150
	    guistrut 1 0
	    guiradio "Max" fullbrightmodels 200
	]
    ]
    guicheckbox "Hit sound" hitsound
    guilist [
	guicheckbox "Force matching player models" forceplayermodels
	guistrut 1 0
	guicheckbox "Always use team skins" teamskins
    ]
    guilist [
	guicheckbox "Outline capture meters		" outlinemeters
	guistrut 1 0
	guicheckbox "Show teammates	" radarteammates 1
	guistrut 1 0
	guicheckbox "Numbered capture bases	" basenumbers
    ]
    guilist [
	guicheckbox "Crosshair color effects		" crosshairfx
	if $crosshairfx [
	    guistrut 1 0
	    guicheckbox "Teammates		" teamcrosshair
	    guistrut 1 0
	    guicheckbox "Hits" hitcrosshair 425
	]
    ]

    guibar
    guibutton "^f3Restore defaults (clears ALL settings)" "exec restore.cfg" "exit"
] 0

newgui graphics_settings [
    guititle "^f7performance key :^f~ ^f0fast^f~, ^f2moderate^f~, ^f3slow and pretty^f~"
    guibar
    if (&& (< $shaders 0) (= $renderpath 0)) [
	guicheckbox "^f2Shaders		"		shaders 1 -1
    ] [
	guilist [
	    guicheckbox "^f2Shaders		"	shaders
	    if $shaders [
		guistrut 1 0
		guiradio "^f0Low detail	"		shaderdetail 1
		guistrut 1 0
		guiradio "^f2High detail"		shaderdetail 3
		//if $hasglsl [
		//    guibar
		//    guicheckbox "^f3GLSL only" forceglsl
		//]
	    ]
	]
    ]
    guilist [
	guitext "Water		"	"blank.png"
	guistrut 1 0
	guicheckbox "^f2Refraction		"		waterrefract
	guistrut 1 0
	guicheckbox "^f3Reflection		"		waterreflect
	guistrut 1 0
	guicheckbox "^f0Caustics	"		caustics
	guistrut 1 0
	guicheckbox "^f0Animation"		vertwater
    ]
    if (> $renderpath 0) [
	guilist [
	    guitext "Waterfalls		"	"blank.png"
	    guistrut 1 0
	    guicheckbox "^f2Refraction		"	waterfallrefract
	    guistrut 1 0
	    guicheckbox "^f0Reflection"	waterfallenv
	]
    ]
    if (= $renderpath 0) [
	guilist [
	    guicheckbox "^f3Shadow maps	"	shadowmap
	    guistrut 1 0
	    if $shadowmap [
		guiradio "^f2Medium quality"	shadowmapsize 9 [blurshadowmap 1]
		guistrut 1 0
		guiradio "^f3High quality"	shadowmapsize 10 [blurshadowmap 2]
	    ] [
		guicheckbox "^f0Blob shadows" blobs
	    ]
	]
	if (>= $maxtmus 3) [
	    guilist [
		guicheckbox "^f2Dynamic lights	"    ffdynlights 5 0
	    ]
	]
    ] [
	guilist [
	    guicheckbox "^f3Soft shadows	"  shadowmap
	    guistrut 1 0
	    if $shadowmap [
		guiradio "^f2Medium quality" shadowmapsize 9 [blurshadowmap 1]
		guistrut 1 0
		guiradio "^f3High quality" shadowmapsize 10 [blurshadowmap 2]
	    ] [
		guicheckbox "^f0Blob shadows" blobs
	    ]
    	]
    	if $glare [
	    glarepreset = 0
	    if (= $glarescale 1) [
		if (= $blurglare 4) [glarepreset = 1]
		if (= $blurglare 7) [glarepreset = 3]
	    ]
	    if (= $glarescale 2) [
		if (= $blurglare 3) [glarepreset = 2]
		if (= $blurglare 7) [glarepreset = 4]
	    ]
	    guilist [
		guicheckbox "^f3Glare		"	glare
		guistrut 1 0
		guiradio "^f2Subtle		"		glarepreset 1 [blurglare 4; glarescale 1]
		guistrut 1 0
		guiradio "^f2Glowy		"		glarepreset 2 [blurglare 3; glarescale 2]
		guistrut 1 0
		guiradio "^f3Soft		"		glarepreset 3 [blurglare 7; glarescale 1]
		guistrut 1 0
		guiradio "^f3Intense"		glarepreset 4 [blurglare 7; glarescale 2]
	    ]
	] [
	    guilist [
		guicheckbox "^f3Glare		"		glare
	    ]
	]
    ]
    if $usetexrect [
	guilist [
	    guicheckbox "^f3Motion blur	"	motionblur
	    if $motionblur [
		guistrut 1 0
		guiradio "^f3Subtle		"		motionblurscale 0.5
		guistrut 1 0
		guiradio "^f3Moderate		"		motionblurscale 0.65
		guistrut 1 0
		guiradio "^f3Intense"		motionblurscale 0.8
	    ]
	]
    ]
    guilist [
	guicheckbox "^f3Grass		"	grass
	if $grass [
	    guistrut 1 0
	    guiradio "^f2Quick fade	"	grassdist 128
	    guistrut 1 0
	    guiradio "^f2Moderate fade	"	grassdist 256
	    guistrut 1 0
	    guiradio "^f3Slow fade"		grassdist 512
	]
    ]
    if (> $renderpath 0) [
	guilist [
	    guicheckbox "^f0Dynamic lights	"	maxdynlights 3 0
	    if $maxdynlights [
		guistrut 1 0
		guiradio "^f0Medium quality"		maxdynlights 3
		guistrut 1 0
		guiradio "^f2High quality"		maxdynlights 5
	    ]
	]
	guilist [
	    guicheckbox "^f0Soft particles	"	depthfx
	    if $depthfx [
		guistrut 1 0
		guiradio "^f0Low quality	"		depthfxsize 7 [depthfxrect 0; depthfxfilter 1; blurdepthfx 1]
		guistrut 1 0
		guiradio "^f2Medium quality"		depthfxsize 10 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
		guistrut 1 0
		guiradio "^f3High quality"		depthfxsize 12 [depthfxrect 1; depthfxfilter 0; blurdepthfx 0]
	    ]
	]
    ]
    guilist [
	guicheckbox "^f0Glass reflect	"		glassenv
    ]
    guilist [
	guicheckbox "^f0Decals		"	decals
	if $decals [
	    guistrut 1 0
	    guiradio "^f0Quick fade	"		decalfade 10000 [maxdecaltris 1024]
	    guistrut 1 0
	    guiradio "^f2Slow fade"		decalfade 60000 [maxdecaltris 4096]
	]
    ]
    guilist [
	guicheckbox "^f0Fix t-joints	"	filltjoints
	guistrut 1 0
	guitext "(world sparklies)"
    ]
    guilist [
	guitext "Textures		"	"blank.png"
	guistrut 1 0
	guiradio "^f0Low quality	"		maxtexsize 256
	guistrut 1 0
	guiradio "^f0Medium quality"	maxtexsize 512
	guistrut 1 0
	guiradio "^f2High quality"	maxtexsize 0
    ]
    guilist [
	guitext "Models		"	"blank.png"
	guistrut 1 0
	guicheckbox "^f0Lighting		"	lightmodels
	guistrut 1 0
	guicheckbox "^f0Reflection		"	envmapmodels
	guistrut 1 0
	guicheckbox "^f0Glow		"		glowmodels
	if (> $renderpath 0) [
	    guistrut 1 0
	    guicheckbox "^f2Bumpmap"	bumpmodels
	]
    ]
    guilist [
	guitext "Animation		"	"blank.png"
	guistrut 1 0
	guiradio "^f0Medium quality"	matskel 1
	guistrut 1 0
	guiradio "^f0High quality"	matskel 0
    ]
] 0

newgui display_settings [
    guicheckbox "V-sync"		vsync 1 0
    guicheckbox "Fullscreen"		fullscreen
    guibar
    guitext "Gamma (default: 100)"
    guislider gamma
    guitext "Full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "Color depth (default: 0)"
    guilistslider colorbits "0 16 24 32"
    guitext "Z-buffer depth (default: 0)"
    guilistslider depthbits "0 16 24 32"
    guitext "Stencil bits (default: 0)"
    guislider stencilbits
    guitext "Anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"
    guibar
    guilist [
	guicheckbox "Bilinear filtering"	bilinear
	guibar
	guicheckbox "Trilinear filtering (mipmaps)"	trilinear
    ]

    guitab "Resolution"
    guitext "Field of view (default: 100)"	"blank.png"
    guislider fov
    guistayopen [
	guilist [
	    guilist [
		guitext "4:3"
		@@@@(resbutton 320 240)
		@@@@(resbutton 640 480)
		@@@@(resbutton 800 600)
		@@@@(resbutton 1024 768)
		@@@@(resbutton 1152 864)
		@@@@(resbutton 1280 960)
		@@@@(resbutton 1400 1050)
		@@@@(resbutton 1600 1200)
		@@@@(resbutton 1792 1344)
		@@@@(resbutton 1856 1392)
		@@@@(resbutton 1920 1440)
		@@@@(resbutton 2048 1536)
		@@@@(resbutton 2800 2100)
		@@@@(resbutton 3200 2400)
	    ]
	    guibar
	    guilist [
		guitext "16:10"
		@@@@(resbutton 320 200)
		@@@@(resbutton 640 400)
		@@@@(resbutton 1024 640)
		@@@@(resbutton 1280 800)
		@@@@(resbutton 1440 900)
		@@@@(resbutton 1600 1000)
		@@@@(resbutton 1680 1050)
		@@@@(resbutton 1920 1200)
		@@@@(resbutton 2048 1280)
		@@@@(resbutton 2560 1600)
		@@@@(resbutton 3840 2400)
	    ]
	    guibar
	    guilist [
		guitext "16:9"
		@@@@(resbutton 1024 600)
		@@@@(resbutton 1280 720)
		@@@@(resbutton 1366 768)
		@@@@(resbutton 1600 900)
		@@@@(resbutton 1920 1080)
		@@@@(resbutton 2048 1152)
		@@@@(resbutton 3840 2160)
	    ]
	    guibar
	    guilist [
		guitext "5:4"
		@@@@(resbutton 600 480)
		@@@@(resbutton 1280 1024)
		@@@@(resbutton 1600 1280)
		@@@@(resbutton 2560 2048)
	    ]
	    guibar
	    guilist [
		guitext "5:3"
		@@@@(resbutton 800 480)
		@@@@(resbutton 1280 768)
		guibar
		guitext "Custom"
		guilist [
		    customw = $scr_w
		    customh = $scr_h
		    guifield customw 4 [scr_w $customw]
		    guifield customh 4 [scr_h $customh]
		]
	    ]
	]
    ]
] "Display"

newgui sound_settings [
    guitext "Sound volume"	"blank.png"
    guislider soundvol
    guitext "Music volume"	"blank.png"
    guislider musicvol
    guitext "Sound channels"	"blank.png"
    guislider soundchans
    guitext "Sound frequency"	"blank.png"
    guilistslider soundfreq "11025 22050 44100"
    guitext "Sound buffer length"	"blank.png"
    guislider soundbufferlen
    guibar
    guicheckbox "Mumble positional audio" mumble
] 0

newgui controls_settings [
    guitext "Basic keybinds, for anything more use the 'bind' command"		"blank.png"
    guitext "Select action to bind and press desired keys (ESC when done):"	"blank.png"
    guistrut 1 0
    guilistsplit n 2 $bindactions [
	guilist [
	    guitext (tabify (concatword $n ": ") 4)
	    newbinds@i = (searchbinds $n)
	    guikeyfield [newbinds@i] -9 [
		oldbinds = (searchbinds [@@n])
		looplist j $oldbinds [bind $j ""]
		looplist j $[newbinds@@i] [bind $j [@@@n]]
	    ]
	]
    ] [guistrut 1 0]
    guitab "Mouse"
    guicheckbox "Invert mouse"		invmouse

    guilist [
	guitext "Mouse sensitivity (game) : "
	newsens = $sensitivity
	guifield newsens 8 [sensitivity $newsens]
    ]

    guilist [
	guitext "Mouse sensitivity (menu) : "
	newguisens = $guisens
	guifield newguisens 8 [guisens $newguisens]
    ]
    guistrut 1 0
    guibar
    guistrut 1 0
    guilist [
	guibutton "^f4Crosshair : " [showgui crosshair]
	guiimage (getcrosshair) [showgui crosshair] 0.5
    ]
    guitext "^f4Crosshair size :"	"blank.png"
    guislider crosshairsize
] "Controls"

newgui console_settings [
    guicheckbox "Chat console"			miniconfilter 0x300 0
    guitext "^f2Console size (lines)"	"blank.png"
    guislider consize
    guitext "Console filter:"
    guilist [
	guilist [
	    guibitfield "Chat"			confilter 0x100
	    guibitfield "Team chat"		confilter 0x200
	    guibitfield "Self frags"		confilter 0x800
	    guibitfield "Other player frags"	confilter 0x1000
	    guibitfield "Team kills"		confilter 0x2000
	]
	guistrut 1 0
	guilist [
	    guibitfield "Warnings"		confilter 0x02
	    guibitfield "Errors"		confilter 0x04
	    guibitfield "Init messages"		confilter 0x10
	    guibitfield "Script messages"	confilter 0x20
	]
	guistrut 1 0
	guilist [
	    guibitfield "Game events"		confilter 0x400
	    guibitfield "Important information"	confilter 0x01
	]
    ]
    guitext "^f2Full console size (percent of screen)"	"blank.png"
    guislider fullconsize
    guitext "Full console filter :"
    guilist [
	guilist [
	    guibitfield "Chat"			fullconfilter 0x100
	    guibitfield "Team chat"		fullconfilter 0x200
	    guibitfield "Self frags"		fullconfilter 0x800
	    guibitfield "Other player frags"	fullconfilter 0x1000
	    guibitfield "Team kills"		fullconfilter 0x2000
	]
	guistrut 1 0
	guilist [
	    guibitfield "Warnings"		fullconfilter 0x02
	    guibitfield "Errors"		fullconfilter 0x04
	    guibitfield "Init messages"		fullconfilter 0x10
	    guibitfield "Script messages"	fullconfilter 0x20
	]
	guistrut 1 0
	guilist [
	    guibitfield "Important information"	fullconfilter 0x01
	    guibitfield "Game events"		fullconfilter 0x400
	]
    ]
    guitab "autoexec.cfg"
    guititle "autoexec.cfg"
    guistrut 1 0
    showfileeditor "autoexec.cfg" "-68" "12"
] "Console"

newgui record_settings [
    guititle "Record settings"
    guibar
    guilist [
	guitext "Name : " "" ""
	guifield movie_name
    ]
    guibar
    guilist [
	guitext "Width : "
	guifield moview  5
	guitext "Height : "
	guifield movieh 5
    ]
    guicheckbox "Sound" moviesound
    guibar
// This is problematic with /movie command : /movie doesn't trigger recording and so button isn't changed. You can still do "Start recording" while you're recording.
    if (= ($recording 1)) [
	guibutton "Start recording" [cleargui; cleargui; movie $movie_name; recording = 1] "sauer"
    ] [
	guibutton "^f4Stop recording" [movie; recording = 0] "sauer"
    ]
] 0

bindactions = [forward backward left right jump attack togglezoom saycommand sayteamcommand showscores toggleconsole screenshot edittoggle "setweapon FI" "setweapon SG" "setweapon CG" "setweapon RL" "setweapon RI" "setweapon GL" "setweapon PI" "weapon" "universaldelta 1" "universaldelta -1" dropflag addbot delbot]

entupdate = [ entset $entguitype $entguiattr0 $entguiattr1 $entguiattr2 $entguiattr3 $entguiattr4 ]

initentgui = [
    entguitype = (enttype)
    @(loopconcat i 5 [result [
	entguiattr@i = (entattr @i)
    ]])
]

genentattributes = [
    entattributes = (loopconcat i (listlen $arg2) [
	entattribname = (at $arg2 $i)
	entattriblimits = (at $arg3 (* 2 $i))
	entattriblimits2 = (at $arg3 (+ 1 (* 2 $i)))
	if (=s (at $entattriblimits 0) $entattriblimits) [
	    result [
		guitext @entattribname
		guislider entguiattr@i @entattriblimits @entattriblimits2 entupdate
	    ]
	] [
	    result [
		guitext @entattribname
		guinameslider entguiattr@i [@@entattriblimits] [@@entattriblimits2] entupdate
	    ]
	]
    ])
]

guilistsplit = [
    guilist [
	i = 0
	l = (listlen $arg3)
	z = (div (+ $l (- $arg2 1)) $arg2)
	loop a $arg2 [
	    guilist [
		t = (min (+ $i $z) $l)
		while [< $i $t] [
		    $arg1 = (at $arg3 $i)
		    arg4
		    i = (+ $i 1)
		]
	    ]
	if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [arg5]
	]
    ]
]

quickeditmenu = [
    editingcheck
    guitext "Quick Commands :"
    guibar
    guilist [
	guitext "Save Map : "
	guifield  savemap_name 10 [ savemap $savemap_name ]
    ]
    guibutton "Quick light"	"calclight -1"
    guibutton "Optimize map"	"remip"
    guibutton "New entity"	"newent shells"
    guibar
    guibutton "^f2New Map" newmap
    guibar
    guibutton help "showgui editing"
]

matmenu = [
    editingcheck
    guibutton "Air"			"editmat air"
    guibutton "Water"			"editmat water"
    guibutton "Lava"			"editmat lava"
    guibutton "Clip"			"editmat clip"
    guibutton "Glass"			"editmat glass"
    guibutton "Noclip"			"editmat noclip"
    guibutton "Gameclip"		"editmat gameclip"
    guibutton "Death"			"editmat death"
    guibutton "Alpha"			"editmat alpha"
    guicheckbox "show material volumes"	showmat
]

brushmenu = [
    guilist [ // diamonds
    guiimage "packages/icons/brush_1c.png"	brush_0
    guiimage "packages/icons/brush_21c.png"	brush_1
    guiimage "packages/icons/brush_421c.png"	brush_2
    ]
    guilist [ // squares
    guiimage "packages/icons/brush_3s.png"	brush_3
    guiimage "packages/icons/brush_5s.png"	brush_4
    guiimage "packages/icons/brush_7s.png"	brush_5
    ]
    guititle "Smooth"
    guilist [ // smooth
    guiimage "packages/icons/brush_3s.png"	brush_6
    guiimage "packages/icons/brush_5s.png"	brush_7
    guiimage "packages/icons/brush_7s.png"	brush_8
    ]
]

newentgui = [
    genentattributes $arg1 $arg2 $arg3
    newgui $arg1 [
	guitext $entguitype
	guibar
	@entattributes
	guitab "Type"
	guilistsplit n 2 $enttypelist [
	    guibutton $n [ entset @n ]
	]
	guitab "Misc"
	@quickeditmenu
    ] "" [initentgui]
]

looplist i $enttypelist [
    newentgui $i "" ""
]

newgui materials [
    @matmenu
    guitab "Misc"
    @quickeditmenu
] "Materials"

newgui brushes [
    @brushmenu
    guitab "Misc"
    @quickeditmenu
] "Brushes"

newgui quickedit [
    @quickeditmenu
    guitab "Materials"
    @matmenu
] "Quick Editing"

newgui entlist [
    editingcheck
    guistrut 1 0
    guilistsplit n 2 $enttypelist [
	guibutton $n [ entset @n ]
    ]
] 0

newentgui light "radius red green blue"	"0 400 0 255 0 255 0 255"
newentgui spotlight "radius"		"0 200"
newentgui playerstart "direction"	"0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag"	"0 360 0 20"
newentgui monster "direction type"	"0 360 0 7"
newentgui mapmodel "direction model"	"0 360 0 100"
newentgui envmap "radius" "0 400"
newentgui jumppad "Z Y X"		"0 200 0 200 0 200"
newentgui sound "type radius size"	"0 20 0 500 0 500"
newentgui particles "type" "0 3"

contexteditgui = [
    if $hmapedit [showgui brushes] [
	if (enthavesel) [
	    showgui (enttype)
	] [showgui (? (havesel) materials quickedit)]
    ]
]

showentgui = [ contexteditgui ] // legacy bind

newgui seltexinfo [
    guitext (concat "Slot :" (getseltex))
    guilist [ loop x 5 [
	texname = (gettexname (getseltex) $x)
	texname = (substr $texname (+ (strstr $texname ">") 1)) // chop off any leading commands
	guiimage (concatword "packages/" $texname) "" 1 1
    ]]
    loop x 5 [ tex = (gettexname (getseltex) $x); if (strcmp $tex "") [] [guitext $tex]]
] 0
